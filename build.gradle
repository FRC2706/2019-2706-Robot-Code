plugins {
    id "java"
    id "edu.wpi.first.GradleRIO" version "2019.1.1-beta-1"
	id "idea"
}

ext.PhoenixVersion = "5.8.1"
ext.AHRSVersion = "3.0.348"

def ROBOT_CLASS = "ca.tean2706.robot.Robot"

// Define my targets (RoboRIO) and artifacts (deployable files)
// This is added by GradleRIO's backing project EmbeddedTools.
deploy {
    targets {
        target("roborio", edu.wpi.first.gradlerio.frc.RoboRIO) {
            failOnMissing = false

            // Team can be overridden by command line, for use with VSCode
            team = getTeamOrDefault(2706)
        }
    }
    artifacts {
        artifact('frcJava', edu.wpi.first.gradlerio.frc.FRCJavaArtifact) {
            targets << "roborio"
            // Debug can be overridden by command line, for use with VSCode
            debug = getDebugOrDefault(false)
        }
    }
}

repositories {
	mavenCentral()
    maven {url "http://first.wpi.edu/FRC/roborio/maven/development"}
    maven {url "http://devsite.ctr-electronics.com/maven/release"}
    maven {url "http://www.kauailabs.com/maven2"}
}

// Defining my dependencies. In this case, WPILib (+ friends)
dependencies {
    compile wpilib()
    compile "com.kauailabs.navx.frc:navx_frc:${AHRSVersion}"
    compile "com.ctre.phoenix:api-java:${PhoenixVersion}"
    compile "com.ctre.phoenix:wpiapi-java:${PhoenixVersion}"
    nativeZip "com.ctre.phoenix:cci:${PhoenixVersion}:linuxathena@zip"
	compile 'com.google.code.gson:gson:2.8.5'
}

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

// Setting up my Jar File. In this case, adding all libraries into the main jar ('fat jar')
// in order to make them all available at runtime. Also adding the manifest so WPILib
// knows where to look for our Robot Class.
jar {
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    manifest edu.wpi.first.gradlerio.GradleRIOPlugin.javaManifest(ROBOT_CLASS)
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.10.2'
}